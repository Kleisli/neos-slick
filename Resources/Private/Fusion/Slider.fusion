prototype(Unikka.Slick:Content.Slider) < prototype(Neos.Fusion:Component) {
    slickProperties = ${ Unikka.Slick.getPropertiesByKeys(node, [
        'draggable', 'infinite', 'pauseOnHover', 'autoplaySpeed', 'speed',
        'autoplay', 'fade', 'arrows', 'dots', 'rtl', 'slidesToShow', 'slidesToScroll'
    ]) }
    responsiveProperties = Unikka.Slick:Content.Slide.ResponsiveConfig.Array {
        @context.node = ${ node }
    }

    renderer =  Neos.Neos:ContentCollection {
        autoplayOverride = Neos.Fusion:RawArray {
            autoplay = ${ Configuration.setting('Unikka.Slick.backend.disableAutoplay') && node.context.inBackend ? false : props.slickProperties.autoplay }
        }
        attributes.class = ${ q(node).property('additionalClass') }
        attributes.data-slick = ${ Array.concat(props.slickProperties, props.responsiveProperties, this.autoplayOverride) }
        attributes.data-slick.@process.toJson = ${Json.stringify(value)}
        attributes.class.@process.collectionClass >

        nodePath = ${ q(node).property('_path') }
    }
}
